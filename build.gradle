plugins {
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    id 'com.jfrog.bintray' version '1.8.1'
    id 'maven-publish'
}

ext{
    bintrayUser = System.getProperty("bintray.user")
    bintrayKey = System.getProperty("bintray.key")
    buildNumber = System.getProperty("build.number")
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java-library'
    apply plugin: 'jacoco'
    apply plugin: 'maven-publish'

    version = '0.0.1'
    sourceCompatibility = '8'

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:2.2.4.RELEASE")
        }
    }

    // common dependencies for all modules
    dependencies {

        // Spring Boot's testing dependencies
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }

        // Lombok
        compileOnly 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        // annotation processor to create configuration metadata
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    javadoc.failOnError = false
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    test {
        useJUnitPlatform {
        }
        systemProperty "aws.accessKeyId", "foo" // dummy AWS credentials because the AWS SDK complains otherwise
        systemProperty "aws.secretKey", "foo"   // dummy AWS credentials because the AWS SDK complains otherwise
    }

    test.finalizedBy jacocoTestReport

    def pomConfig = {
        licenses {
            license {
                name "MIT"
                url "https://opensource.org/licenses/MIT"
                distribution "repo"
            }
        }
        developers {
            developer {
                id "thombergs"
                name "Tom Hombergs"
                email "tom.hombergs@gmail.com"
            }
        }

        scm {
            url "https://github.com/thombergs/sqs-starter"
        }
    }

    publishing {
        publications {
            mavenPublication(MavenPublication) {
                from components.java
                artifact sourcesJar {
                    classifier "sources"
                }
                artifact javadocJar {
                    classifier "javadoc"
                }
                groupId 'io.reflectoring'
                artifactId 'sqs-starter'
                version project.version
                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', 'A Spring Boot starter for robust SQS message handling and publishing.')
                    root.appendNode('name', 'sqs-starter')
                    root.appendNode('url', 'https://github.com/thombergs/sqs-starter')
                    root.children().last() + pomConfig
                }
            }
        }
    }

    bintray {
        user = bintrayUser
        key = bintrayKey
        publications = ['mavenPublication']

        pkg {
            repo = 'maven-releases'
            name = 'sqs-starter'
            userOrg = 'reflectoring'
            licenses = ['MIT']
            vcsUrl = 'https://github.com/thombergs/sqs-starter'
            version {
                name = project.version
                desc = "build ${buildNumber}"
                released  = new Date()
            }
        }

        publish = true
    }


}
